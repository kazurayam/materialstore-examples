plugins {
    id 'org.asciidoctor.jvm.convert' version '3.1.0'
    id 'org.asciidoctor.jvm.gems' version '3.1.0'
}

repositories {
    mavenCentral()
    ruby.gems()
}

asciidoctor {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    sources {
        include 'index.adoc'
    }
    resources {
        from('src/docs/asciidoc') {
            include 'Chapter*/images/*'
        }
    }
    asciidoctorj {
        attributes('stylesdir': '@style',
                'stylesheet': 'asciidoctor.css')
    }
}

asciidoctorj {
    modules {
        diagram.use()
        diagram.version '1.5.16'
    }
    requires = [
        'asciidoctor-diagram'
    ]
    attributes 'source-highlighter': 'coderay'
}

task cleanDocs(type: Delete) {
    delete fileTree('docs') {
        include '**/*'
    }
}

defaultTasks 'clean', 'docs'

task docs(dependsOn: [
        cleanDocs,
        asciidoctor
]) doLast {
    copy {
        from 'build/docs/asciidoc/index.html'
        into 'docs'
    }
    copy {
        from 'src/docs/asciidoc/'
        include 'Chapter*/images/*'
        into 'docs'
    }
}

tasks.getByPath("asciidoctor").mustRunAfter("cleanDocs")
