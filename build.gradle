plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'idea'
}

group = 'com.kazurayam'
archivesBaseName = 'materialstore-examples'
version = '0.1.0-SNAPSHOT'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

ext {
    groovyVersion = '2.4.20'   // same version as Katalon Studio 7.9.1
    seleniumjavaVersion = '3.141.59'
    webdrivermanager = '4.4.3'
    ashotVersion = '1.5.4'
}
repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

dependencies {

    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'

    // https://mvnrepository.com/artifact/com.kazurayam/subprocessj
    implementation group: 'com.kazurayam', name: 'subprocessj', version: '0.1.0'

    // https://mvnrepository.com/artifact/com.google.guava/guava
    implementation group: 'com.google.guava', name: 'guava', version: '23.0'

    implementation "org.seleniumhq.selenium:selenium-java:${seleniumjavaVersion}"

    // https://mvnrepository.com/artifact/io.github.bonigarcia/webdrivermanager
    implementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: "${webdrivermanager}"

    //-------------------------------------------------------------------------

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    // https://mvnrepository.com/artifact/org.jsoup/jsoup
    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.7.2'

    // https://mvnrepository.com/artifact/com.kazurayam/subprocessj
    testImplementation group: 'com.kazurayam', name: 'materialstore', version: '0.1.0-SNAPSHOT'

    // https://mvnrepository.com/artifact/com.kazurayam/ashotwrapper
    testImplementation group: 'com.kazurayam', name: 'ashotwrapper', version: '0.1.0-SNAPSHOT'



}

test {
    useJUnitPlatform()
}

compileTestJava {
    // options.incremental = true
    // options.fork = true
    // options.failOnError = false
    options.compilerArgs.add("-Xlint:deprecation")
}

javadoc {
    options.encoding = 'UTF-8'
    options.locale = "en_US"
}
task copyJavadoc(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
javadoc.finalizedBy copyJavadoc

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'Examples how to use the materialstore library in automated tests'
                url = 'https://kazurayam.github.io/materialstore-examples/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}


signing {
    sign publishing.publications.mavenJava
}
// I do not want to sign the SNAPSHOT
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}